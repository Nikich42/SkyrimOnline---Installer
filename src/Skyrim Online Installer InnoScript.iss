; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Skyrim Online Mod"
#define MyAppVersion "r3"
#define MyAppPublisher "Skyrim Online"
#define MyAppURL "http://skyrim.ophelia-core.com/"

#define ExternalsFilePath "..\bin\ext\"
#define DllFilePath "..\bin\root\"
#define LicenseFilePath "..\installer_license.rtf"
#define OutputDirPath "..\"
#define SkyrimSaveLocation "{userdocs}\My Games\Skyrim"
#define SkyrimSaveBackupLocation "{userdesktop}\SkyrimDataBackup"
#define SkyrimDir "{reg:HKLM\SOFTWARE\Bethesda Softworks\Skyrim,Installed Path}"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{719B4740-7A1C-42B7-BA78-0CB2486EE714}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={#SkyrimDir}
DefaultGroupName=Skyrim Online Mod
DirExistsWarning=no
LicenseFile={#LicenseFilePath}
OutputDir={#OutputDirPath}
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
ShowComponentSizes=yes
AlwaysShowComponentsList=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Types]
Name: full; Description: "Full installation"
Name: mod; Description: "Mod only"
Name: custom; Description: "Custom"; Flags: iscustom

[Tasks]
;Optional install of Server capability
Name: "LaunchServerINI"; Description: "Launch server INI after install. This can be edited later by going to {app}\GameServer.ini"; Flags: unchecked

;Optional Data Backup
Name: "BackupData"; Description: "Backup save and config files to the desktop (Recommended) - Skyrim data must be in {#SkyrimSaveLocation}"
Name: "URLLaunch"; Description: "Allow URL launching of the game"

[Components]
;Necessary Prerequisites
Name: "Prereqs"; Description: "Prerequisites"; Types: full mod
Name: "Prereqs\DotNetInstall"; Description: "Microsoft .NET 4.5"; Types: full mod
Name: "Prereqs\VSRedistInstall"; Description: "Visual Studio 2012 redist"; Types: full mod
Name: "Prereqs\XNAInstall"; Description: "Microsoft XNA 4.0 redist"; Types: full mod
Name: "Prereqs\SKSELaunch"; Description: "Link to install SKSE"; Types: full mod

;Only install the mod with no server hosting capability, leaves prereqs checked by default
Name: "Mod"; Description: "Mod"; Types: full mod
Name: "Mod\SkyrimModInstall"; Description: "Skyrim Online Mod"; Types: full mod

;Optional server hosting capability
Name: "AdditionalComponents"; Description: "Additional Components"; Types: full
Name: "AdditionalComponents\ServerInstall"; Description: "Server hosting capability"; Types: full

[Files]
;Main mod DLLs
Source: "{#DllFilePath}\*"; DestDir: "{app}"; Flags: ignoreversion; Components: Mod\SkyrimModInstall
Source: "{#DllFilePath}Data\OBSE\Plugins\Oblivion.Online.dll"; DestDir: "{app}\Data\OBSE\Plugins"; Flags: ignoreversion; Components: Mod\SkyrimModInstall
Source: "{#DllFilePath}Data\Online\UI\*"; DestDir: "{app}\Data\Online\UI"; Flags: ignoreversion; Components: Mod\SkyrimModInstall
Source: "{#DllFilePath}Modules\Game.Client.dll"; DestDir: "{app}\Modules"; Flags: ignoreversion; Components: Mod\SkyrimModInstall

;External Dependencies
Source: "{#ExternalsFilePath}dotNetFx45_Full_setup.exe"; DestDir: "{%TEMP}\Ext"; Flags: ignoreversion deleteafterinstall; Components: Prereqs\DotNetInstall 
Source: "{#ExternalsFilePath}vcredist_x86.exe"; DestDir: "{%TEMP}\Ext"; Flags: ignoreversion deleteafterinstall; Components: Prereqs\VSRedistInstall
Source: "{#ExternalsFilePath}xnafx40_redist.msi"; DestDir: "{%TEMP}\Ext"; Flags: ignoreversion deleteafterinstall; Components: Prereqs\XNAInstall
Source: "{#ExternalsFilePath}Skyrim Script Extender (SKSE).url"; DestDir: "{%TEMP}\Ext"; Flags: ignoreversion deleteafterinstall; Components: Prereqs\SKSELaunch

;Server Files
Source: "{#DllFilePath}ServerInstall\*"; DestDir: "{app}\SkyrimServer"; Flags: ignoreversion; Components: AdditionalComponents\ServerInstall

;Backup Skyrim Data Locations
Source: "{#SkyrimSaveLocation}\*"; DestDir: "{#SkyrimSaveBackupLocation}"; Flags: ignoreversion external confirmoverwrite; Tasks: BackupData
Source: "{#SkyrimSaveLocation}\Saves\*"; DestDir: "{#SkyrimSaveBackupLocation}\Saves"; Flags: ignoreversion external confirmoverwrite; Tasks: BackupData

 
[Icons]
;Start Menu Shortcut to the mod uninstall wizard
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; Components: Mod\SkyrimModInstall

;Desktop Icon to Launch a custom Skyrim Online Server
Name: "{userdesktop}\Launch Skyrim Online Server"; Filename: "{app}\SkyrimServer\Game.Server.exe"; Components: AdditionalComponents\ServerInstall

[Run]
;Launch installations of external dependencies
Filename: "{%TEMP}\Ext\dotNetFx45_Full_setup.exe"; Components: Prereqs\DotNetInstall
Filename: "{%TEMP}\Ext\vcredist_x86.exe"; Components: Prereqs\VSRedistInstall                                
Filename: "{%TEMP}\Ext\xnafx40_redist.msi";  Flags: shellexec waituntilterminated; Components: Prereqs\XNAInstall 
Filename: "{%TEMP}\Ext\Skyrim Script Extender (SKSE).url";  Flags: shellexec waituntilterminated; Components: Prereqs\SKSELaunch

;Launch server INI after install
Filename: "{app}\SkyrimServer\GameServer.ini"; Flags: shellexec waituntilterminated; Description: "Launch the Server INI"; Tasks: LaunchServerINI

[Dirs]
;Custom directories
Name: "{app}\Data\OBSE"
Name: "{app}\Data\OBSE\Plugins"
Name: "{app}\Data\Online"
Name: "{app}\Data\Online\UI"
Name: "{%TEMP}\Ext"
Name: "{app}\SkyrimServer"

[Registry]
Root: "HKCR"; Subkey: "skyrimonline"; ValueType: string; ValueName: "URL Protocol"; ValueData: "URL:skyrimonline Protocol"; Flags: createvalueifdoesntexist uninsdeletekey; Tasks: URLLaunch
Root: "HKCR"; Subkey: "skyrimonline\Shell\Open\Command"; ValueType: string; ValueData: """{#SkyrimDir}\skse_loader.exe"""; Flags: createvalueifdoesntexist uninsdeletekey; Tasks: URLLaunch
Root: "HKCR"; Subkey: "skyrimonline\DefaultIcon"; ValueType: string; ValueData: "{#SkyrimDir}\TESV.exe"; Flags: createvalueifdoesntexist uninsdeletekey; Tasks: URLLaunch
